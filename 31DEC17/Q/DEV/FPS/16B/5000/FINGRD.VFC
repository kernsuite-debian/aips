      DEFINE FINGRD(U,ROW,MO2,LROW,TYPE)
"-----------------------------------------------------------------------
"! FPS VFC routine: Finish griding a row of uv data.
"# AP-appl UV
"-----------------------------------------------------------------------
";  Copyright (C) 1995
";  Associated Universities, Inc. Washington DC, USA.
";
";  This program is free software; you can redistribute it and/or
";  modify it under the terms of the GNU General Public License as
";  published by the Free Software Foundation; either version 2 of
";  the License, or (at your option) any later version.
";
";  This program is distributed in the hope that it will be useful,
";  but WITHOUT ANY WARRANTY; without even the implied warranty of
";  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
";  GNU General Public License for more details.
";
";  You should have received a copy of the GNU General Public
";  License along with this program; if not, write to the Free
";  Software Foundation, Inc., 675 Massachusetts Ave, Cambridge,
";  MA 02139, USA.
";
";  Correspondence concerning AIPS should be addressed as follows:
";         Internet email: aipsmail@nrao.edu.
";         Postal address: AIPS Project Office
";                         National Radio Astronomy Observatory
";                         520 Edgemont Road
";                         Charlottesville, VA 22903-2475 USA
"-----------------------------------------------------------------------
"-----------------------------------------------------------------------
      LOCAL ROW2,CROW,CRWI,SYM,REND,LROW1,CROW2
"
"     FINGRD Does various tasks associated with completion of gridding
"     a row.  If U is within 1/2 support of 0 the symmetric row is
"     conjugated, flipped and added.  Next two conjugate rows are
"     formed, vis1 + i*vis2 (for beams, vis2 is the sampling function=
"     the sum of the weights).  Finally rows are rotated so that
"     zero column (assumed LROW/2+1) goes to the first column.
"     If U=0 the space for the next row down is used.
"
"     CALLING SEQUENCE - CALL FINGRD(U,ROW,MO2,LROW,TYPE)
"                        U = U in cells (non-negative)
"                        ROW = Base address of Grid row of interest
"                        MO2 = Half the number of rows kept in the AP.
"                        LROW = Length of row (no. vis)
"                        TYPE = 1 for IPOL,IBEM maps
"                               2 for Q,U maps
"                               3 for V maps.
"                               4 for two line maps.
"
"     Expects necessary constants in following AP locations:
"           0 = COS(PHASE0)           to shift map center
"           1 = SIN(PHASE0)
"           2 = COS(DELPHR)           for rotating down rows
"           3 = SIN(DELPHR)
"           4 = COS(DELPHC)           for rotating down columns
"           5 = SIN(DELPHC)
"           6 = 1.0
"           7 = 0.0
"
"     Programmer = W. D. Cotton, July 1980.
"
"
"           SET ADDRESSES
"
      SP09 = LROW + LROW
      CROW = ROW + SP09
      CROW2 = CROW + 2
      LROW1 = LROW - 1
"                                       For VPOL zero weights
      IF TYPE <> 3 GOTO FIRST
         CRWI = CROW + 1
         CALL VMOV (CRWI,2,CROW,2,LROW)
"                                       Check if near origin.
FIRST: IF U > MO2 GOTO HIGH
"                                       Near origin, add symmetric points.
      ROW2 = ROW + 2
      SP02 = SP09 + SP09
      SP03 = U + U
      SP05 = SP02 * SP03
      SYM = ROW - SP05
"                                       If U=0 move 0 row 1 slot lower and
"                                       work from there.
      IF U > 0 GOTO NOT0
      SYM = ROW - SP02
      CRWI = SP02
      CALL VMOV(ROW,1,SYM,1,CRWI)
NOT0: REND = SYM + SP09
      REND = REND - 2
"                                       First visibility.
      CALL CVJADD(ROW2,2,REND,-2,ROW2,2,LROW1)
      CALL CVJADD(ROW,2,SYM,2,ROW,2,1)              "First cell.
"                                       Second visibility
      REND = REND + SP09
      SYM = SYM + SP09
      CALL CVJADD (CROW2,2,REND,-2,CROW2,2,LROW1)
      CALL CVJADD (CROW,2,SYM,2,CROW,2,1)
"                                       Form conjugate rows.
HIGH: CALL GRDMIX (ROW,CROW,LROW)
"                                       Rotate zero to first column.
      SYM = CROW + LROW
      CALL VSWAP(CROW,1,SYM,1,LROW)
      SYM = ROW + LROW
      CALL VSWAP(ROW,1,SYM,1,LROW)
"                                       Rotate map center.
      CALL PHSROT(ROW,2,ROW,2,0,2,LROW)    "Rotate row
      CALL VNEG(1,2,1,2,2)                 "Conj. phase rotation
      CALL PHSROT(CROW,2,CROW,2,0,2,LROW)  "Rotate conjugate row
"                                       Reverse Conjugate row.
      CRWI = CROW + 3
      CALL VRVRS(CROW2,2,LROW1)         "Reverse reals
      CALL VRVRS(CRWI,2,LROW1)         "Reverse imaginaries.
      CALL VNEG(1,2,1,2,2)             "Undo conj. of phase
      CALL PHSROT(0,2,0,2,4,6,1)       "Rotate PHASE0 for next row.
S999: END
