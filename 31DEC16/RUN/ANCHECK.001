$---------------------------------------------------------------
$! $ Check antenna files for By sign
$# RUN POPS VLBI
$-----------------------------------------------------------------------
$;  Copyright (C) 2009-2010
$;  Associated Universities, Inc. Washington DC, USA.
$;
$;  This program is free software; you can redistribute it/or
$;  and/or modify it under the terms of the GNU General Public
$;  License as published by the Free Software Foundation; either
$;  version 2 of the License, or (at your option) any later
$;  version.
$;
$;  This program is distributed in the hope that it will be
$;  useful, but WITHOUT ANY WARRANTY; without even the implied
$;  warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
$;  PURPOSE.  See the GNU General Public License for more
$;  details.
$;
$;  You should have received a copy of the GNU General Public
$;  License along with this program; if not, write to the Free
$;  Software Foundation, Inc., 675 Massachusetts Ave, Cambridge,
$;  MA 02139, USA.
$;
$;  Correspondence concerning AIPS should be addressed as
$;  follows:
$;         Internet email: aipsmail@nrao.edu.
$;         Postal address: AIPS Project Office
$;                         National Radio Astronomy Observatory
$;                         520 Edgemont Road
$;                         Charlottesville, VA 22903-2475 USA
$-----------------------------------------------------------------------
PROC DECLS
STRING*8 YMINUS(34)
STRING*8 YPLUS(22), ANNAME
STRING*16 ARNAME
SCALAR NROW
FINISH
PROC ANCHECK
$                                set values
YMINUS = 'SC','GB','HN','AR','RO','YS','JB','DEF','KNO','DAR'
YMINUS(11)~'MK2','TAB','BR','FD','KP','LA','NL','OV','PT','Y'
YMINUS(21)~'MK','GO','VLBA_SC','VLBA_HN','PV','PICOVEL'
YMINUS(27)~'VLBA_BR','VLBA_FD','VLBA_KP','VLBA_LA','VLBA_NL'
YMINUS(32)~'VLBA_OV','VLBA_PT','VLBA_MK'
YPLUS = 'EB','NT','WB','UR','MC','ON','CAM', 'SH', 'HH'
YPLUS(10)~'AT','PA','PKS','TI','HOB','CD','CED','MOP','CAT','EF'
YPLUS(20)~'EFLSBERG','ONSALA60','PDBURE'
inext = 'AN'
$                                         by array name
KEYW='ARRNAM'; GETTHEAD; ARNAME=KEYSTR;
KEYW='ARRAYY'; GETTHEAD;
if (arname = 'vla') then;
   if (prtlev>0) then;
      print arname, keyv;
      end
   if (keyv(1)>0) then;
     keyv(1)=-keyv(1); keyv(2)=-keyv(2); x = -x
     PUTTHEAD; print arname, keyv(1), 'corrected'
     end
   RETURN
   end
if (arname = 'gmrt') then;
   if (prtlev>0) then;
      print arname, keyv;
      end
   if (keyv(1)<0) then;
      keyv(1)=-keyv(1); keyv(2)=-keyv(2); x = -x
      PUTTHEAD; print arname, keyv(1), 'corrected'
      end
   RETURN
   end
$                                         by station name
KEYW='NUM ROW'; GETTHEAD; NROW = KEYV(1)
FOR I=1:NROW;
   pixxy = I,1,1; tabget; anname=keystr; pixxy= i,2,2; tabget
   if (prtlev>0) then;
      print 'row',i, anname,keyv;
      end
   for j=1:34;
      if (anname = yminus(j)) then
         x = j
         if (keyv(1)>0) then
            keyv(1)=-keyv(1); keyv(2)=-keyv(2); x = -x
            tabput; print anname, keyv(1), 'corrected'
            end
         end
      end
   for j=1:22
      if (anname = yplus(j)) then
         x = j + 34
         if (keyv(1)<0) then
            keyv(1)=-keyv(1); keyv(2)=-keyv(2); x = -x
            tabput; print anname, keyv(1), 'corrected'
            end
         end
      end
    if (x = 0) then; print anname, 'not recognized'; end
    end

return
finish
